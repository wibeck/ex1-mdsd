import 'componentModel.ecore'
import 'assembly.ecore'
import 'environment.ecore'



context assembly::AssemblyConnector
inv assemblyContextsNotEqual : (not (self.providedrole.assemblyContext = self.requiredrole.assemblyContext)) = true

	
-- the source of the interface of a required-role is the component's service that requires it
context assembly::RequiredRole
	inv: self.assemblyContext.instantiates.interfaceServiceMap ->exists(entry | entry.service -> select(service| service.required 
		-> includes(self.interface)))  -> notEmpty() and not (self.assemblyContext.instantiates = null)

-- ensures that there are services for each signature of the provided interfaces of a component		
context componentModel::Component
	inv: self.interfaceServiceMap ->select(entry | entry.service -> select(service| service.correspondence -> union(entry.providedInterface.signatures)
		))  -> notEmpty()

-- the source of the interface of a provided-role is the component's provided-interface
context assembly::ProvidedRole
	inv: self.assemblyContext.instantiates.interfaceServiceMap ->exists(entry | entry.providedInterface = self.interface)
	
-- assembly contexts, that are connected via asssembly connector are  allocated to the same container or their containers are linked
context assembly::AssemblyConnector
	inv:let contextsOfRoles =  Set{self.providedrole.assemblyContext, self.requiredrole.assemblyContext},
	containerOfRoles = environment::Container -> collect(allocationcontext) -> select(ac | contextsOfRoles ->includes(ac.assemblycontext))
	in (containerOfRoles -> size() = 1 )xor (environment::Link -> collect(container) -> includesAll(containerOfRoles) = true)
	
-- only top-level components (such that are not instantiated with a composite component) are allowed to be allocated; nested components are 
-- implicitly allocated
context environment::AllocationContext
	inv: self.assemblycontext.ownerComponent = null
	
-- the provided and required interfaces of assemblyContexts stem from their roles	
context assembly::AssemblyContext
	inv: self.requiredInterfaces = self.requiredRoles -> collect(interface) and self.providedInterfaces = self.providedRoles -> collect(interface)

-- the role of RequiredDelegationConnector can only be of type RequiredRole
context assembly::RequiredDelegationConnector
	inv: (self.role.oclIsKindOf(RequiredRole))

-- the role of ProvidedDelegationConnector can only be of type ProvidedRole	
context assembly::ProvidedDelegationConnector
	inv: self.role.oclIsKindOf(assembly::ProvidedRole)
